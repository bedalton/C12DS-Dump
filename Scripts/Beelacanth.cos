* Beelacanth 

* Launches 5 seeds
* makes boing sound when making seeds
* the launcher animaes when launching
* seeds split
* plant grows out of seed

* C1 Beelacanth Seed		2	3	32300	
* C1 Beelacanth			2	4	32300	
* C1 Seed Launcher 		2 	23 	32301
* C1 Plant Killer (Cloud)	2	19	32300	
* C1 Plant Killer (Tool)	2	24	32301	

inst
*	create the seed launcher
new: simp 2 23 32304 "c1beelacanth" 7 11 600
attr 199
accg 5
bhvr 0
elas 0
fric 100
perm 60

doif game "C1_to_DS_Loaded" = 1 and totl 2 23 32304 <= 1
*	C1 to DS
	setv va00 3832
	setv va01 11062
elif type game "ds_game_type" ne 2
* 	it's NOT a string, this must be C3 only
* 	choose a safe C3 location
	setv va00 5440
	setv va01 3580
	velo rand 30 40 -5
else
* 	it IS a string, but what is the value?
	doif game "ds_game_type" eq "undocked"
* 		this is an undocked world, location needs to be DS safe
		setv va00 6100
		setv va01 9200
		velo rand 30 40 -5
	elif game "ds_game_type" eq "docked"
* 		this is a docked world, location can be anywhere in the DS/C3 realm
		setv va00 6100
		setv va01 9200
		velo rand 30 40 -5
	endi
endi
mvsf va00 va01

* create solution X beelacanth killer spray
new: simp 2 24 32301 "c1beelacanth" 1 18 700
attr 199
accg 5
bhvr 0
elas 0
fric 100
perm 60
doif game "C1_to_DS_Loaded" = 1 and totl 2 23 32300 <= 1
*	C1 to DS
	mvsf 3892 11062
elif type game "ds_game_type" ne 2
* 	it's NOT a string, this must be C3 only
* 	choose a safe C3 location
	setv va00 5440
	setv va01 3580
	velo rand 30 40 -5
	mvsf va00 va01
else
* 	it IS a string, but what is the value?
	doif game "ds_game_type" eq "undocked"
* 		this is an undocked world, location needs to be DS safe
		setv va00 6100
		setv va01 9200
		velo rand 30 40 -5
		mvsf va00 va01
	elif game "ds_game_type" eq "docked"
* 		this is a docked world, location can be anywhere in the DS/C3 realm
		setv va00 6100
		setv va01 9200
		velo rand 30 40 -5
		mvsf va00 va01
	endi
endi


scrp 2 3 32300 1
*	seed is pushed
	inst
	doif game "C1_Clickable_Food" = 1
		doif from = pntr
			setv va00 posl
			setv va01 post
			reps 1
				new: simp 2 3 32300 "c1beelacanth" 1 10 590
				attr 199
				accg .5
				bhvr 48
				elas 25
				fric 75
				tick rand 801 901
				perm 60
				aero 10
				rnge 500
				mvsf va00 va01
				velo rand -15 15 rand -8 -4
			repe
		endi
	endi
endm

scrp 2 3 32300 6
	inst
	snde "c1c4"
endm

scrp 2 3 32300 9
*	Beelacanth seed timer
	inst
	doif movs = 0
		setv va00 posl
		setv va01 post
		addv va00 -8
		addv va01 2
		setv va02 rtyp grap va00 va01
		setv va03 0
		esee 2 4 32300
			addv va03 1
		next
		doif va02 = 5 or va02 = 6 or va02 = 7 and va03 <= 7
*		safe place to germinate into plant
			new: simp 2 4 32300 "c1beelacanth" 10 0 591
			attr 192
			accg .5
			bhvr 0
			elas 25
			fric 75
			tick rand 801 901
			perm 60
			mvsf va00 va01
		endi
		targ ownr
		kill targ
	endi
endm

scrp 2 3 32300 12
*	Beelacanth seed is eaten
	inst
	lock
	targ from
	doif crea targ = 1
*		chem 5 1.0
*		chem 34 0.75
*		chem 93 0.5
		snde "chwp"
		stim writ from 79 1.0
	endi
	targ ownr
	kill targ
endm

scrp 2 4 32300 9
*	Beelacanth plant timer
	inst
	doif pose < 9
		setv va00 pose
		addv va00 1
		pose va00
	elif pose = 9
		frat 25
		anim [9 5 4 3 2]
		over
		kill ownr
	endi

*	post anim change	
	doif pose = 8
*		create seed(s)
		esee 2 4 32300
			addv va03 1
		next
		setv va04 7
		subv va04 va03
		doif va04 < 1
			setv va04 1
		endi
		targ ownr
		setv va00 posl
		setv va01 post
		addv va00 8
		addv va01 10
		setv va05 0
		reps va04
			targ ownr
			new: simp 2 3 32300 "c1beelacanth" 1 10 590
			attr 199
			accg .5
			bhvr 48
			doif va05 = 0
				bhvr 0
				setv va05 1
			endi
			elas 25
			fric 75
			tick rand 801 901
			perm 60
			aero 10
			rnge 500
			mvsf va00 va01
			velo rand -15 15 rand -8 -4
		repe
	endi
endm

scrp 2 19 32300 9
*	Solution X cloud timer
	inst
	lock
*	kill any plants or seeds that you touch
	etch 2 3 32300
		kill targ
	next
	etch 2 4 32300
		kill targ
	next
	addv ov00 1
	doif ov00 = 20
		slow
		anim [10 11 12 13]
		over
		inst
		targ ownr
		kill targ
	elif ov00 > 20
		targ ownr
		kill targ
	endi
endm

scrp 2 23 32304 1
*	Beelacanth seed launcher is pushed
	inst
	lock
	frat 2.0
	reps 5
		anim [0 1 2 3 4 5 6]
		over
		inst
		snde "c1h4"
		new: simp 2 3 32300 "c1beelacanth" 1 10 590
		attr 199
		accg .5
		bhvr 48
		elas 25
		fric 75
		aero 10
		rnge 500
		tick rand 801 901
		perm 60
		seta va02 targ
		targ ownr
		setv va00 posl
		setv va01 post
		addv va01 5
		addv va00 28
		targ va02
		mvsf va00 va01
		velo rand -15 15 rand -8 -4
		targ ownr
		anim [5 4 3 2 1 0]
		wait 10
		inst
	repe
endm

scrp 2 23 32304 6
*	beelacanth seed launcher collides
	sndc "c1d2"
endm

scrp 2 24 32301 1
*	Solution X tool is activated
	inst
	snde "c1sx"
	setv va00 posl
	setv va01 post
	subv va01 20
	subv va00 10
	reps rand 5 7
		new: simp 2 19 32300 "c1beelacanth" 14 19 800
		attr 192
		accg .5
		aero 0
		fric 0
		elas 0
		tick rand 2 3
		mvsf va00 va01
		perm 60
		setv ov00 0
		anim [0 1 2 3 4 5 6 7 8 9 255 3]
		velo rand -5.0 5.0 rand -2.0 -1.0
		wait rand 5 9
		inst
	repe
endm

scrp 2 24 32301 6
*	solution x collides
	sndc "c1d2"
endm

rscr
enum 2 3 32300
	kill targ
next
scrx 2 3 32300 1
scrx 2 3 32300 6
scrx 2 3 32300 9
scrx 2 4 32300 12

enum 2 4 32300
	kill targ
next
scrx 2 4 32300 9

enum 2 23 32304
	kill targ
next
scrx 2 23 32304 1
scrx 2 23 32304 6

enum 2 19 32300
	kill targ
next
scrx 2 19 32300 9

enum 2 24 32301
	kill targ
next
scrx 2 24 32301 1
scrx 2 24 32301 6