* C1 Laburnum

* Dependencies:
*  C1Plants.c16


inst
lock

* 	Laburnum Plant
new: vhcl 2 5 32303 "c1plants" 1 31 100
cabn 20 4 43 27
attr 735
accg 5
fric 100
aero 10
elas 0
bhvr 0
perm 60
tick rand 900 1000
mesg wrt+ targ 1000 0 0 10

doif game "C1_to_DS_Loaded" = 1 and totl 2 5 32303 <= 1
	mvsf 2462 10547

elif type game "ds_game_type" ne 2
* 		it's NOT a string, this must be C3 only
* 		choose a safe C3 location
	setv va00 5440
	setv va01 3580
	velo rand 30 40 -5
	mvsf va00 va01
else
* 		it IS a string, but what is the value?
	doif game "ds_game_type" eq "undocked"
* 			this is an undocked world, location needs to be DS safe
		setv va00 6100
		setv va01 9200
		velo rand 30 40 -5
		mvsf va00 va01
	elif game "ds_game_type" eq "docked"
* 			this is a docked world, location can be anywhere in the DS/C3 realm
		setv va00 6100
		setv va01 9200
		velo rand 30 40 -5
		mvsf va00 va01
	endi
endi

***********
* Scripts

scrp 2 5 32303 1
* 	Laburnum plant clicked
	inst
	doif game "C1_Clickable_Food" = 1
		mesg wrt+ ownr 1000 1 0 0
	endi
endm

scrp 2 5 32303 9
* 	Laburnum plant timer
	inst
	mesg wrt+ ownr 1000 0 0 0
endm

scrp 2 5 32303 125
* 	Laburnum plant lost flower
	inst
	mesg wrt+ ownr 1000 0 0 rand 20 90
endm

scrp 2 5 32303 1000
*	Laburnum plant's timer
	inst
	setv va20 0
	epas 2 5 32401
		addv va20 1
	next

	doif va20 < 1 or _p1_ = 1
*		no flower exists, so better make one
		setv va00 posl
		setv va01 post
		new: simp 2 5 32401 "c1plants" 2 32 101
		addv va00 20
		addv va01 12
		mvsf va00 va01
		seta va20 targ
		accg 0
		attr 199
		fric 100
		elas 0
		aero 100
		bhvr 48
		perm 60
		targ ownr
		doif _p1_ <> 1
			setv va52 0
			loop
				spas ownr va20
				wait 1
				inst
				addv va52 1
				targ va20
			untl targ = null or carr = ownr or va52 > 10
			inst
		else
			targ va20
			pose 1
			velo rand -10 10 rand 1 5
		endi
		attr 199
		accg 3
		fric 90
		aero 10
	endi
endm

scrp 2 5 32401 1
*	Laburnum weed clicked
	inst
	doif game "C1_Clickable_Food" = 1
		setv va00 posl
		setv va01 post
		new: simp 2 5 32401 "c1plants" 2 32 101
		attr 199
		elas 0
		bhvr 48
		perm 60
		pose 1
		accg 3
		fric 90
		aero 10
		mvsf va00 va01
		velo rand -15 15 rand -15 -5
	endi
endm

scrp 2 5 32401 4
*	Laburnum - I've been picked up
	inst
	lock
	pose 1
	mira rand 0 1
endm

scrp 2 5 32401 12
*	eating a weed
*	Laburnum
	inst
	lock
	seta va20 norn
	targ from
	doif crea targ = 1
		chem 73 1.0
		chem 74 1.0
		chem 71 0.9
		chem 72 0.7
*		chem 155 .5
		snde "chwp"
		stim writ from 83 1
	endi
	targ ownr
	kill targ
endm

rscr

enum 2 5 32303
	kill targ
next
scrx 2 5 32303 1
scrx 2 5 32303 9
scrx 2 5 32303 125
scrx 2 5 32303 1000

enum 2 5 32401
	kill targ
next
scrx 2 5 32401 1
scrx 2 5 32401 4
scrx 2 5 32401 12