****************************
* C1 robot
****************************

* Dependencies:
*  c1robot.c16
*  c1sq.wav
*  c1d2.wav

* Installers
inst

new: simp 2 21 32305 "c1robot" 8 0 200
attr 199
accg 5
bhvr 25
fric 12.5
aero 10
elas 25
perm 60

doif game "C1_to_DS_Loaded" = 1 and totl 2 21 32305 <= 1
* 	robot near hives
	mvsf 2922 10536
elif type game "ds_game_type" ne 2
* 	it's NOT a string, this must be C3 only
* 	choose a safe C3 location
	setv va00 5440
	setv va01 3580
	velo rand 30 40 -5
	mvsf va00 va01
else
* 	it IS a string, but what is the value?
	doif game "ds_game_type" eq "undocked"
* 		this is an undocked world, location needs to be DS safe
		setv va00 6100
		setv va01 9200
		velo rand 30 40 -5
		mvsf va00 va01
	elif game "ds_game_type" eq "docked"
* 		this is a docked world, location can be anywhere in the DS/C3 realm
		setv va00 6100
		setv va01 9200
		velo rand 30 40 -5
		mvsf va00 va01
	endi
endi

****************************
* Scripts

scrp 2 21 32305 1
*	robot is pushed
	inst
	lock
	stim writ from 97 1
	doif ov00 = 0
		setv ov00 1
		anim [0 1 2 3 255]
		sndl "c1sq"
		tick 20
		mesg writ ownr 9
	endi
endm

scrp 2 21 32305 3
*	robot is hit
	inst
	lock
	stim writ from 0 1
endm

scrp 2 21 32305 4
*	robot is pisked up	
	stpc
	setv ov00 0
endm

scrp 2 21 32305 6
*	robot collides
	inst
	lock
	doif velx > 0
		mira 1
	elif velx < 0
		mira 0
	endi
	sndc "c1d2"
endm

scrp 2 21 32305 9
*	robot timer
	inst
	lock
	doif ov00 = 1
		doif mira = 0
			mira 0
			velo -5 0
		else
			mira 1
			velo 5 0
		endi
		addv ov00 1
	elif ov00 = 2
		doif mira = 0
			mira 1
			velo 5 0
		else
			mira 0
			velo -5 0
		endi
		addv ov00 1
	else
		setv ov00 0
		velo 0 0
		stpc
		pose 0
	endi
endm

scrp 2 21 32305 12
*	robot is bitten
	inst
	lock
	stim writ from 0 1
endm

****************************
* Removers

* robot remover
rscr
enum 2 21 32305
	kill targ
next
scrx 2 21 32305 1
scrx 2 21 32305 3
scrx 2 21 32305 4
scrx 2 21 32305 6
scrx 2 21 32305 12