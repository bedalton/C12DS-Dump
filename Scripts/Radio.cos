****************************
* C1 Radio
****************************

* Dependencies:
*  c1radio.c16
*  c1r4.wav
*  c1d2.wav
*  c1r6.wav
*  c1r7.wav

* Installers
inst

new: simp 2 21 32311 "c1radio" 0 0 200
attr 199
accg 5
bhvr 25
fric 12.5
aero 10
elas 0
perm 60
frat 1.5

doif game "C1_to_DS_Loaded" = 1 and totl 2 21 32311 <= 1
* 	Radio in music room
	mvsf 8687 10604
elif type game "ds_game_type" ne 2
* 	it's NOT a string, this must be C3 only
* 	choose a safe C3 location
	setv va00 5440
	setv va01 3580
	velo rand 30 40 -5
	mvsf va00 va01
else
* 	it IS a string, but what is the value?
	doif game "ds_game_type" eq "undocked"
* 		this is an undocked world, location needs to be DS safe
		setv va00 6100
		setv va01 9200
		velo rand 30 40 -5
		mvsf va00 va01
	elif game "ds_game_type" eq "docked"
* 		this is a docked world, location can be anywhere in the DS/C3 realm
		setv va00 6100
		setv va01 9200
		velo rand 30 40 -5
		mvsf va00 va01
	endi

endi

****************************
* Scripts


scrp 2 21 32311 1
*	Radio is pushed
	inst
	lock
	stim writ from 97 1
	stpc
	setv va00 rand 0 2
	doif va00 = 0
		sndc "c1r4"
		anim [0 1 2 3 2 1 2 3 2 1 0 1 2 3 3 2 1 0 1 2 3 2 1 2 3 2 1 0 1 2 3 3 2 1]
		over
		wait 30
	elif va00 = 1
		anim [3 2 1]
		over
		sndc "c1r6"
		wait 40
	elif va00 = 2
		anim [2 3 2 1 0]
		over
		sndc "c1r7"
		wait 40
	endi
endm

scrp 2 21 32311 3
*	Radio is hit
	inst
	lock
	stim writ from 0 1
	velo rand -10 10 rand -10 -20
	sndc "c1d2"
	setv ov00 0
endm

scrp 2 21 32311 4
*	Radio is picked up	
endm

scrp 2 21 32311 5
*	Radio is dropped
endm

scrp 2 21 32311 6
*	Radio collides
	inst
	lock
	doif ov00 = 0
		doif vely < 10 or vely > 10
			stpc
			sndc "c1d2"
		elif vely > -2 and vely < 2
			stpc
		endi
	endi
endm

scrp 2 21 32311 12
*	Radio is bitten
	inst
	lock
	stim writ from 0 1
	setv ov00 0
endm

****************************
* Removers

* Radio remover
rscr
enum 2 21 32311
	kill targ
next
scrx 2 21 32311 1
scrx 2 21 32311 3
scrx 2 21 32311 4
scrx 2 21 32311 6
scrx 2 21 32311 12