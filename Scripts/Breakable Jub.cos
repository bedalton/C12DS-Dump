****************************
* C1 BreakableJug
****************************

* Dependencies:
*	c1ba.wav
*	c1d2.wav
*	C1BreakableJug.c16
*	Breakable jug: 2 21 32316
*	Breakable jug vendor: 2 23 32316

* Installers
inst

new: vhcl 2 23 32305 "c1breakablejugvendor" 1 0 100
attr 711
accg 7
bhvr 1
fric 90
aero 0
elas 20
perm 70
cabn 0 0 58 71
doif game "C1_to_DS_Loaded" = 1 and totl 2 21 32316 <= 1
* 	new Breakable Jug in Ettin house
	mvsf 6323 11068
elif type game "ds_game_type" ne 2
* 	it's NOT a string, this must be C3 only
* 	choose a safe C3 location
	setv va00 5440
	setv va01 3580
	velo rand 5 10 -2
	mvsf va00 va01
else
* 	it IS a string, but what is the value?
	doif game "ds_game_type" eq "undocked"
* 		this is an undocked world, location needs to be DS safe
		setv va00 6100
		setv va01 9200
		velo rand 5 10 -2
		mvsf va00 va01
	elif game "ds_game_type" eq "docked"
* 		this is a docked world, location can be anywhere in the DS/C3 realm
		setv va00 6100
		setv va01 9200
		velo rand 5 10 -2
		mvsf va00 va01
	endi
endi

****************************
* Scripts

scrp 2 21 32316 1
*	BreakableJug is pushed
	inst
	lock
	stim writ from 97 1
	doif carr <> null
		doif type carr = 6
			rpas carr ownr
		elif type carr = 7
			mesg writ carr 23
		endi
	endi
	setv ov90 posl
	setv ov91 post
	velo rand -20 20 rand -30 -20
endm

scrp 2 21 32316 4
*	Breakable Jug is picked up
endm

scrp 2 21 32316 6
*	Breakable Jug collides
	inst
	doif vely > 3 or vely < -3 and obst down <= 10
		tick rand 30 40
		velo 0 0
		elas 0
		sndc "c1ba"
		attr 192
		bhvr 0
		anim [1 2 3 4]
		over
	elif vely > 3 or vely < -3 and obst down > 10
		snde "c1d2"
	endi
endm

scrp 2 21 32316 9
*	breakable jug timer
	inst
	addv ov99 1
	doif ov99 > 1
		targ ownr
		kill targ
	endi
endm

scrp 2 23 32305 1
*	vendor pushed
	inst
	stim writ from 90 1.0
	setv va02 0
	epas 0 0 0
		addv va02 1
	next
	doif va02 < 10
		sndc "c1v1"
		setv va00 posl
		setv va01 post
		addv va00 5
		addv va01 45
		new: simp 2 21 32316 "c1breakablejug" 7 0 200
		attr 199
		accg 3
		bhvr 33
		fric 90
		aero 0
		elas 40
		perm 70
		mvsf va00 va01
		spas ownr targ
		velo 0 0
	endi
endm

scrp 2 23 32305 6
*	jug vendor collides
	stpc
	doif vely > 3 or vely < -3 and obst down <= 10
		sndc "c1d2"
		epas 2 21 32316
			doif targ <> null
				inst
				rpas ownr targ
				tick rand 30 40
				velo 0 0
				elas 0
				sndc "c1ba"
				attr 192
				bhvr 0
				anim [1 2 3 4]
			endi
		next
	endi
endm


****************************
* Removers

* BreakableJug remover
rscr
enum 2 21 32316
	kill targ
next
scrx 2 21 32316 1
scrx 2 21 32316 4
scrx 2 21 32316 6
scrx 2 21 32316 9

enum 2 23 32305
	kill targ
next
scrx 2 23 32305 1
scrx 2 23 32305 6