****************************
* C1 Trumpet
****************************

* Dependencies:
*  c1trumpet.c16
*  c1t1.wav
*  c1t2.wav
*  c1d2.wav


* Installers
inst

new: simp 2 21 32312 "c1trumpet" 0 0 200
attr 199
accg 5
bhvr 25
fric 12.5
aero 10
elas 0
perm 60
puhl 0 22 34
puhl 1 22 34
pupt 0 22 34
pupt 1 22 34

doif game "C1_to_DS_Loaded" = 1 and totl 2 21 32312 <= 1
* 	Trumpet in music room
	mvsf 2930 10812
elif type game "ds_game_type" ne 2
* 	it's NOT a string, this must be C3 only
* 	choose a safe C3 location
	setv va00 5440
	setv va01 3580
	velo rand 30 40 -5
	mvsf va00 va01
else
* 	it IS a string, but what is the value?
	doif game "ds_game_type" eq "undocked"
* 		this is an undocked world, location needs to be DS safe
		setv va00 6100
		setv va01 9200
		velo rand 30 40 -5
		mvsf va00 va01
	elif game "ds_game_type" eq "docked"
* 		this is a docked world, location can be anywhere in the DS/C3 realm
		setv va00 6100
		setv va01 9200
		velo rand 30 40 -5
		mvsf va00 va01
	endi
endi

****************************
* Scripts


scrp 2 21 32312 1
*	Trumpet is pushed
	inst
	lock
	stim writ from 97 1
	stpc
	doif rand 0 1 = 1
		sndc "c1t1"
	else
		sndc "c1t2"
	endi
endm

scrp 2 21 32312 3
*	Trumpet is hit
	inst
	lock
	stim writ from 0 1
	velo rand -10 10 rand -10 -20
	sndc "c1d2"
	setv ov00 0
endm

scrp 2 21 32312 4
*	Trumpet is picked up	
	pose 1
endm

scrp 2 21 32312 5
*	Trumpet is dropped
	pose 0
endm

scrp 2 21 32312 6
*	Trumpet collides
	inst
	lock
	doif ov00 = 0
		doif vely < 10 or vely > 10
			stpc
			sndc "c1d2"
		elif vely > -2 and vely < 2
			stpc
		endi
	endi
	pose 0
endm

scrp 2 21 32312 12
*	Trumpet is bitten
	inst
	lock
	stim writ from 0 1
	setv ov00 0
endm

****************************
* Removers

* Trumpet remover
rscr
enum 2 21 32312
	kill targ
next
scrx 2 21 32312 1
scrx 2 21 32312 3
scrx 2 21 32312 4
scrx 2 21 32312 6
scrx 2 21 32312 12