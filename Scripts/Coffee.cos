* C1 Coffee

* Dependencies:
*  C1CoffeePot.c16
*  C1d2.wav
*  C1d3.wav

* Provides energy, a little warmth, and reduces tiredness and sleepiness.

**************************
* Installers

doif game "C1_to_DS_Loaded" = 1 and totl 2 25 32302 <= 1
	new: simp 2 25 32302 "c1coffeepot" 2 0 200
	attr 199
	pose 1
	bhvr 48
	elas 1
	accg 5
	fric 95
	perm 60

*	C1 to DS
	setv ov62 1
	mvto 3863 10820
	etch 1 7 32332
		doif type targ = 6
			gpas 2 25 32302 0
		endi
	next
elif type game "ds_game_type" ne 2
* 	it's NOT a string, this must be C3 only
* 	choose a safe C3 location
	setv va00 5440
	setv va01 3580
	velo rand 30 40 -5
	mvsf va00 va01
else
* 	it IS a string, but what is the value?
	doif game "ds_game_type" eq "undocked"
* 		this is an undocked world, location needs to be DS safe
		setv va00 6100
		setv va01 9200
		velo rand 30 40 -5
		mvsf va00 va01
	elif game "ds_game_type" eq "docked"
* 		this is a docked world, location can be anywhere in the DS/C3 realm
		setv va00 6100
		setv va01 9200
		velo rand 30 40 -5
		mvsf va00 va01
	endi

endi
**************************
* Scripts

scrp 2 25 32302 1
*	Coffee gets clicked
	inst
	doif game "C1_Clickable_Food" = 1
		setv va00 posl
		setv va01 post
		new: simp 2 25 32302 "c1coffeepot" 2 0 200
		attr 199
		pose 1
		bhvr 48
		elas 1
		accg 5
		fric 95
		perm 60
		mvsf va00 va01
		velo rand -15 15 rand -15 -5
	endi
endm

scrp 2 25 32302 4
*	picked up the coffee
	inst
	lock
	doif from = pntr
		pose 0
	endi
endm

scrp 2 25 32302 5
*	dropping the coffee
	inst
	lock
	pose 1
endm

scrp 2 25 32302 6
*	coffee collides
	inst
	lock
	sndc "c1d2"
endm

scrp 2 25 32302 12
*	eating the coffee
	inst
	lock
	targ ownr
	anim [1 0]
	over
	sndc "c1d3"
	anim [0 1]
	stim writ from 26 1.0
	targ from
	chem 3 .25
	chem 1 .25
	chem 5 .25
	chem 12 .25
	chem 34 .25
	chem 75 -1.0
	chem 93 .25
	chem 100 .25
	chem 113 .25
	chem 152 -0.125
	chem 153 0.125
	chem 154 -1.0
	chem 155 -1.0
	chem 204 .25
*	targ from
*	zomb 0
endm


**************************
* Removers
rscr
enum 2 25 32302
	kill targ
next
scrx 2 25 32302 1
scrx 2 25 32302 4
scrx 2 25 32302 5
scrx 2 25 32302 12