****************************
* C1 Drum
****************************

* Dependencies:
*  C1Drum.c16
*  C1d4.wav
*  C1d2.wav


* Installers
inst

new: simp 2 21 32308 "c1drum" 0 0 200
attr 199
accg 5
bhvr 25
fric 12.5
aero 10
elas 0
perm 60

doif game "C1_to_DS_Loaded" = 1 and totl 2 21 32308 <= 1
* 	Drum in music room
	mvsf 2885 10812
elif type game "ds_game_type" ne 2
* 	it's NOT a string, this must be C3 only
* 	choose a safe C3 location
	setv va00 5440
	setv va01 3580
	velo rand 30 40 -5
	mvsf va00 va01
else
* 	it IS a string, but what is the value?
	doif game "ds_game_type" eq "undocked"
* 		this is an undocked world, location needs to be DS safe
		setv va00 6100
		setv va01 9200
		velo rand 30 40 -5
		mvsf va00 va01
	elif game "ds_game_type" eq "docked"
* 		this is a docked world, location can be anywhere in the DS/C3 realm
		setv va00 6100
		setv va01 9200
		velo rand 30 40 -5
		mvsf va00 va01
	endi
endi

****************************
* Scripts


scrp 2 21 32308 1
*	Drum is pushed
	inst
	lock
	stim writ from 97 1
	stpc
	sndc "c1d4"
endm

scrp 2 21 32308 3
*	Drum is hit
	inst
	lock
	stim writ from 0 1
	velo rand -10 10 rand -10 -20
	sndc "c1d2"
	setv ov00 0
endm

scrp 2 21 32308 4
*	Drum is picked up	
	stpc
endm

scrp 2 21 32308 6
*	Drum collides
	inst
	lock
	doif ov00 = 0
		doif vely < 10 or vely > 10
			stpc
			sndc "c1d2"
		elif vely > -2 and vely < 2
			stpc
		endi
	endi
endm

scrp 2 21 32308 12
*	Drum is bitten
	inst
	lock
	stim writ from 0 1
	setv ov00 0
endm

****************************
* Removers

* Drum remover
rscr
enum 2 21 32308
	kill targ
next
scrx 2 21 32308 1
scrx 2 21 32308 3
scrx 2 21 32308 4
scrx 2 21 32308 6
scrx 2 21 32308 12