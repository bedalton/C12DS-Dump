* C1 Pyrethium

* Dependencies:
*  c1plants.c16
*  chwp.wav

inst
lock

* 	Pyrethium plant
new: vhcl 2 5 32301 "c1plants" 1 6 200
attr 735
cabn 10 -3 40 30
accg 5
fric 100
aero 10
elas 0
bhvr 0
perm 60
tick rand 400 1000
mesg wrt+ targ 1001 0 0 10


doif game "C1_to_DS_Loaded" = 1 and totl 2 5 32301 <= 1
	mvsf 5510 10873

* hide the Pyrethium pot
	new: simp 1 19 32398 "c1plants" 1 39 0
	attr 0
	accg 0
	fric 100
	aero 10
	elas 0
	bhvr 0

elif type game "ds_game_type" ne 2
* 		it's NOT a string, this must be C3 only
* 		choose a safe C3 location
	setv va00 5440
	setv va01 3580
	velo rand 30 40 -5
	mvsf va00 va01
else
* 		it IS a string, but what is the value?
	doif game "ds_game_type" eq "undocked"
* 			this is an undocked world, location needs to be DS safe
		setv va00 6100
		setv va01 9200
		velo rand 30 40 -5
		mvsf va00 va01
	elif game "ds_game_type" eq "docked"
* 			this is a docked world, location can be anywhere in the DS/C3 realm
		setv va00 6100
		setv va01 9200
		velo rand 30 40 -5
		mvsf va00 va01
	endi
endi

***********
* Scripts

scrp 2 5 32301 1
*	Pyrethium plant clicked
	inst
	doif game "C1_Clickable_Food" = 1
		mesg wrt+ targ 1001 1 0 0
	endi
endm

scrp 2 5 32392 1
*	Pyrethium weed clicked
	inst
	lock
	doif game "C1_Clickable_Food" = 1
		setv va00 posl
		setv va01 post
		new: simp 2 5 32392 "c1plants" 2 7 101
		addv va00 10
		addv va01 10
		seta va20 targ
		fric 100
		aero 10
		elas 0
		bhvr 48
		perm 60
		pose 1
		accg 3
		attr 199
		fric 90
		aero 10
		mvsf va00 va01
		velo rand -10 10 rand -10 -5
	endi
endm

scrp 2 5 32301 9
*	Pyrethium plant timer
	inst
	doif movs = 0
		mesg wrt+ targ 1001 0 0 0
	endi
endm


scrp 2 5 32301 125
*	Pyrethium weed lost flower
	inst
	mesg wrt+ ownr 1001 0 0 rand 10 100
endm


scrp 2 5 32301 1001
*	Pyrethium plant's stuff
	inst
	doif movs = 0
		setv va20 0
		epas 2 5 32392
			addv va20 1
		next
		doif va20 < 1 or _p1_ <> 0
*		no flower exists, so better make one
			setv va00 posl
			setv va01 post
			new: simp 2 5 32392 "c1plants" 2 7 101
			addv va00 10
			addv va01 10
			seta va20 targ
			fric 100
			aero 10
			elas 0
			bhvr 48
			perm 60
			mvsf va00 va01
			targ ownr
			doif _p1_ <> 1
*			growing
				targ va20
				setv va21 0
				loop
					targ ownr
					spas ownr va20
					wait 1
					inst
					targ va20
					addv va21 1
				untl carr = ownr or va21 > 100
				targ va20
			else
*			vending		
				targ va20
				pose 1
				velo rand -10 10 rand 1 5
			endi
			targ va20
			accg 3
			attr 199
			fric 90
			aero 10
		endi
	else
		mesg wrt+ ownr 1001 0 0 rand 10 40
	endi
endm

scrp 2 5 32392 4
*	Pyrethium picked up
	pose 1
endm

scrp 2 5 32392 12
*	eating a weed
*	Pyrethium
	inst
	lock
	seta va20 norn
	targ from
	doif crea targ = 1
		chem 148 1.0
		chem 149 1.0
		chem 150 1.0
		chem 151 1.0
		chem 5 0.9
*		chem 68 0.25
*		chem 34 0.50
		snde "chwp"
		stim writ from 83 1
	endi
	targ ownr
	kill targ
endm

rscr

enum 1 19 32398
	kill targ
next

enum 2 5 32301
	kill targ
next
scrx 2 5 32301 1
scrx 2 5 32301 9
scrx 2 5 32301 125
scrx 2 5 32301 1001



enum 2 5 32392
	kill targ
next
scrx 2 5 32392 1
scrx 2 5 32392 4
scrx 2 5 32392 12