* C1 Nightshade

* Dependencies:
*  c1plants.c16
*  chwp.wav

inst
lock

new: vhcl 2 5 32302 "c1plants" 1 9 100
attr 735
cabn 8 1 46 39
accg 5
fric 100
aero 10
elas 0
bhvr 0
perm 60
tick rand 900 1000
mesg wrt+ targ 1000 0 0 0

doif game "C1_to_DS_Loaded" = 1 and totl 2 5 32302 <= 1
	mvsf 7104 10465

elif type game "ds_game_type" ne 2
* 		it's NOT a string, this must be C3 only
* 		choose a safe C3 location
	setv va00 5440
	setv va01 3580
	velo rand 30 40 -5
	mvsf va00 va01
else
* 		it IS a string, but what is the value?
	doif game "ds_game_type" eq "undocked"
* 			this is an undocked world, location needs to be DS safe
		setv va00 6100
		setv va01 9200
		velo rand 30 40 -5
		mvsf va00 va01
	elif game "ds_game_type" eq "docked"
* 			this is a docked world, location can be anywhere in the DS/C3 realm
		setv va00 6100
		setv va01 9200
		velo rand 30 40 -5
		mvsf va00 va01
	endi
endi

***********
* Scripts

scrp 2 5 32302 1
*	Nightshade plant clicked
	inst
	doif game "C1_Clickable_Food" = 1
		mesg wrt+ ownr 1000 1 0 0
	endi
endm

scrp 2 5 32302 9
*	Nightshade plant timer
	inst
	mesg wrt+ ownr 1000 9 0 0
endm

scrp 2 5 32302 125
*	Nightshade plant lost weed
	inst
	mesg wrt+ ownr 1000 0 0 rand 20 110
endm

scrp 2 5 32302 1000
*	Nightshade plant's timer
	inst
	setv va20 0
	epas 2 5 32393
		addv va20 1
	next

	doif va20 < 1 or _p1_ = 1
*		no flower exists, so better make one
		setv va00 posl
		setv va01 post
		new: simp 2 5 32393 "c1plants" 2 10 101
		puhl 0 16 16
		puhl 1 16 16
		addv va00 10
		addv va01 10
		seta va20 targ
		mvsf va00 va01
		accg 0
		attr 199
		fric 100
		elas 0
		aero 100
		bhvr 48
		perm 60
		targ ownr
		doif _p1_ <> 1
			setv va52 0
			loop
				spas ownr va20
				wait 1
				inst
				addv va52 1
				targ va20
			untl targ = null or carr = ownr or va52 > 10
			inst
		else
			targ va20
			pose 1
			velo rand -10 10 rand 1 5
		endi
		accg 5
		fric 100
		aero 10
		attr 199

	endi
endm

scrp 2 5 32393 1
*	Nightshade clicked
	inst
	doif game "C1_Clickable_Food" = 1
		setv va00 posl
		setv va01 post
		new: simp 2 5 32393 "c1plants" 2 10 101
		puhl 0 16 16
		puhl 1 16 16
		addv va00 10
		addv va01 10
		attr 199
		accg 5
		fric 100
		elas 0
		aero 100
		bhvr 48
		perm 60
		fric 100
		aero 10
		attr 199
		pose 1
		mvsf va00 va01
		velo rand -10 10 rand -10 -5
	endi
endm

scrp 2 5 32393 4
*	I've been picked up
*	Nightshade
	inst
	lock
	pose 1
	mira rand 0 1
endm

scrp 2 5 32393 12
*	eating a weed
*	Nightshade
	inst
	lock
	seta va20 norn
	targ from
	doif crea targ = 1
*		chem 71 0.5
		chem 5 0.9
		chem 155 1.0
		chem 154 1.0
		snde "chwp"
		stim writ from 83 1
	endi
	targ ownr
	kill targ
endm

rscr
enum 2 5 32302
	kill targ
next
scrx 2 5 32302 1
scrx 2 5 32302 9

enum 2 5 32393
	kill targ
next
scrx 2 5 32393 1
scrx 2 5 32393 4
scrx 2 5 32393 12