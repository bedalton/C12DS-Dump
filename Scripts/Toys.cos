****************************
* C1 Toys
****************************

* Dependencies:
*  c1b4.wav
*  c1ball.c16
*  c1c2.wav
*  c1cannon.c16
*  c1cf.wav
*  c1d2.wav
*  c1d7.wav
*  c1pump.c16
*  c1water.c16
*  c1pi.wav
*  c1piano.c16
*  c1to.wav
*  c1top.c16
*  c1s3.wav splash
* 2005-06-12 Added splashes to ball and cannonball

* Installers
inst

new: comp 2 21 32314 "c1ball" 7 0 100
attr 199
accg 3
bhvr 33
fric 90
aero 0
elas 75
pose 6
perm 70
tick 2

doif game "C1_to_DS_Loaded" = 1 and totl 2 21 32314 <= 1

* new ball in norn house
	mvsf 3707 10705

* new ball in Grendel tree
	new: comp 2 21 32314 "c1ball" 7 0 100
	attr 199
	accg 3
	bhvr 33
	fric 90
	aero 0
	elas 75
	pose 6
	perm 70
	tick 2
	mvsf 6690 10499

* new ball on island
	new: comp 2 21 32314 "c1ball" 7 0 100
	attr 199
	accg 3
	bhvr 33
	fric 90
	aero 0
	elas 75
	pose 6
	perm 70
	tick 2
	mvsf 8755 10641

* Cannon
	new: comp 2 21 32300 "c1cannon" 2 0 200
	attr 199
	bhvr 1
	fric 100
	accg 25
	aero 0
	elas 0
	pose 0
	perm 65
	tick 0
	mvsf 1680 10517

* pump cover up
	new: simp 1 5 32333 "c1pump" 1 4 0
	attr 0
	mvto 4224 10814

* actual pump
	new: comp 2 21 32333 "c1pump" 4 0 200
	attr 4
	bhvr 1
	fric 100
	aero 0
	elas 0
	pat: dull 1 "c1water" 0 83 41 1
	part 1
	pose 0
	mvto 4203 10814

* pianola
	new: vhcl 2 21 1021 "c1piano" 0 0 5000
	cabp 1
	cabw 100
	cabn 20 -50 130 0
	attr 719
	bhvr 1
	fric 100
	accg 3
	elas 0
	perm 60
	mvsf 6054 10800

* Top
	new: simp 2 21 1160 "c1top" 0 0 5000
	attr 199
	bhvr 41
	accg 5
	elas 0
	fric 90
	aero 10
	perm 70
*	tick 1
	pose 0
	setv ov99 0
	setv ov98 180
	addv ov91 5
	mvsf 4131 10878
	setv ov90 posl
	setv ov91 post
elif type game "ds_game_type" ne 2
* 	it's NOT a string, this must be C3 only
* 	choose a safe C3 location
	setv va00 5440
	setv va01 3580
	velo rand 30 40 -5
	mvsf va00 va01
else
* 	it IS a string, but what is the value?
	doif game "ds_game_type" eq "undocked"
* 		this is an undocked world, location needs to be DS safe
		setv va00 6100
		setv va01 9200
		velo rand 30 40 -5
		mvsf va00 va01
	elif game "ds_game_type" eq "docked"
* 		this is a docked world, location can be anywhere in the DS/C3 realm
		setv va00 6100
		setv va01 9200
		velo rand 30 40 -5
		mvsf va00 va01
	endi
endi

****************************
* Scripts

scrp 2 21 32314 1
*	ball is pushed
	lock
	sndc "c1b4"
	setv va00 posl
	addv va00 22
	targ from
	doif targ <> null
		setv va02 posl
	else
		setv va02 va00
	endi
	setv va10 va02
	subv va10 va00
	mulv va10 -1
	doif va10 > 10
		setv va10 10
	elif va10 < -10
		setv va10 -10
	endi
	targ ownr
	anim [0 1 2 3 4 5 6 255]
*	velo rand -5 5 rand -30 -10
	velo va10 rand -30 -10
	stim writ from 97 1
endm

scrp 2 21 32314 4
*	ball is picked up
	lock
	pose 6
	stpc
endm

scrp 2 21 32314 6
*	Ball collides
	inst
*	tick 20
	setv va00 velx
	absv va00
	setv va01 vely
	absv va01

	doif va00 > 2 or va01 > 2
		doif ov99 <> 1
			sndc "c1b4"
		endi
		anim [0 1 2 3 4 5 6 255]
	else
		pose 6
	endi

	doif velx = 0 and vely = 0
		pose 6
	endi
endm

scrp 2 21 32314 9
	inst
	doif velx < 1 and velx > -1 and vely < 1 and vely > -1
		pose 6
	endi
	setv va00 posl
	setv va01 post
	addv va01 10
	setv va02 posr
	setv va03 posb
	subv va03 10
	setv ov20 rtyp grap va00 va01
	setv ov21 rtyp grap va02 va01
	setv ov22 rtyp grap va00 va03
	setv ov23 rtyp grap va02 va03
	addv ov24 30
	doif ov24 >= 360
		setv ov24 0
	endi
	doif ov20 = 8 and ov21 = 8 and ov22 = 8 and ov23 = 8
*		float up in water
		doif ov99 = 0
			setv ov99 1
			sndc "c1s3"
		endi
		pose 6
		accg -4
		aero 40
	elif ov20 = 9 and ov21 = 9 and ov22 = 9 and ov23 = 9
*		float up in water
		doif ov99 = 0
			setv ov99 1
			sndc "c1s3"
		endi
		pose 6
		accg -4
		aero 40
	elif ov22 = 8 and ov23 = 8 and ov20 <> 8 and ov21 <> 8
*		bobbing on the water surface
		pose 6
		accg 0
		velo 0 sin_ ov24
	elif ov22 = 9 and ov23 = 9 and ov20 <> 9 and ov21 <> 9
*		bobbing on the water surface
		pose 6
		accg 0
		velo 0 sin_ ov24
	elif ov22 <> 8 and ov23 <> 8 and ov22 <> 9 or ov23 <> 9
*		normal (out of water)
		setv ov99 0
		accg 3
		aero 0
	endi
endm

scrp 2 21 32300 1
*	cannon is pushed
	inst
*	lock
	stim writ from 97 1
	setv va00 posl
	setv va01 post
	setv va21 mira
	stpc
	anim [1 1 1 1 1 0]
	sndc "c1c2"
	new: simp 2 21 32315 "c1cannon" 1 9 198
	attr 199
	accg 10
	fric 0
	aero 10
	elas 25
	perm 10
*	tick 100
	tick 2
	doif va21 = 0
		subv va00 35
		subv va01 14
		velo -10 -3
		setv va02 va00
		setv va03 va01
		addv va02 20
		addv va03 20
	else
		addv va00 90
		subv va01 14
		velo 10 -3
		setv va02 va00
		setv va03 va01
		addv va02 20
		addv va03 20
	endi
	mvto va02 va03
	new: simp 2 21 32300 "c1cannon" 7 2 199
	anim [1 2 3 4 5 6 0]
	mvto va00 va01
	over
	targ ownr
	pose 0
endm

scrp 2 21 32300 4
*	pick up cannon
	inst
	lock
	tick 1

endm

scrp 2 21 32300 5
*	drop cannon
	inst
	lock
	tick 0
endm

scrp 2 21 32300 6
*	cannon collides
	inst
	snde "c1d2"
endm

scrp 2 21 32300 9
*	drop cannon
	inst
	lock
	doif velx > 0
		mira 1
	elif velx < 0
		mira 0
	endi
endm

scrp 2 21 32315 6
*	cannonball collides
	inst
	doif vely > 1 or vely < -1
		snde "c1d7"
	endi
endm

scrp 2 21 32315 9
*	cannonball timer
	inst
	addv ov00 1
	doif ov00 > 200
		targ ownr
		kill targ
	endi
	setv va00 posl
	setv va01 post
	addv va01 7
	setv va02 posr
	setv va03 posb
	subv va03 7
	setv ov20 rtyp grap va00 va01
	setv ov21 rtyp grap va02 va01
	setv ov22 rtyp grap va00 va03
	setv ov23 rtyp grap va02 va03

	doif ov20 = 8 and ov21 = 8 and ov22 = 8 and ov23 = 8
*		you hit water
		doif ov99 = 0
			setv ov99 1
			stpc
			sndc "c1s3"
		endi
	elif ov20 = 9 and ov21 = 9 and ov22 = 9 and ov23 = 9
*		you hit water
		doif ov99 = 0
			setv ov99 1
			stpc
			sndc "c1s3"
		endi
	elif ov22 <> 8 and ov23 <> 8 and ov22 <> 9 or ov23 <> 9
*		normal (out of water)
		setv ov99 0
		accg 3
		aero 0
	endi
endm

scrp 2 21 32333 1
*	pump is pushed
	lock
	doif ov99 <> 1
		stim writ from 97 1
		setv ov99 1
		reps 1
			part 0
			anim [0 1 2 3 3]
			over
			part 1
			anim [ 1 2 3 4 5 0]
			sndc "c1cf"
			part 0
			anim [3 2 1 0]
			over
		repe
		setv ov99 0
	endi
endm

scrp 2 21 1021 1
*	pianola is pushed
	inst
	lock
	stim writ from 97 1
	anim [0 1 2 3 4 5 0 1 2 3 4 5 0 1 2 3 4 5 0 1 2 3 4 5 0 1 2 3 4 5 0 1 2 3 4 5 0 1 2 3 4 5 0 1 2 3 4 5 0 1 2 3 4 5 0 1 2 3 4 5 0 1 3 5 0]
	sndc "c1pi"
	over
	stpc
endm

scrp 2 21 1021 124
*	pianola receives a passenger
	inst
	dpas 4 0 0
	dpas 3 4 0
endm

scrp 2 21 1160 1
*	top is pushed
	doif ov99 = 0
		tick 1
		setv ov97 0
		setv ov99 1
		sndc "c1to"
		stim writ from 97 1
	endi
endm

scrp 2 21 1160 4
*	top picked up
	setv ov99 0
	stpc
	pose 0
endm

scrp 2 21 1160 6
*	top collides
	setv ov99 0
	stpc
	pose 0
endm


scrp 2 21 1160 9
*	top timer
	inst
	doif ov99 = 0
		tick 0
		stpc
		pose 0
		doif movs = 0
			mvsf ov90 ov91
		endi
	else
		doif ov97 = 0
			anim [0 1 2 3 4 5 6 7 255]
			setv ov90 posl
			setv ov91 post
			addv ov91 5
			addv ov97 1
		elif ov97 > 0 and ov97 < 120
			setv va00 ov90
			setv va10 sin_ ov98
			mulv va10 40
			addv va00 va10
			addv ov98 7.5
			doif ov98 >= 360
				setv ov98 0
			endi
			setv va11 va10
			absv va11
			doif va10 < 0 and obst left >= va11
				setv va00 ov90
				setv va01 post
				addv va01 5
				addv va00 va10
			elif va10 > 0 and obst rght >= va11
				setv va00 ov90
				setv va01 post
				addv va01 5
				addv va00 va10
			else
				setv va00 posl
				setv va01 post
				addv va01 5
			endi
			doif movs = 0 and carr = null
				mvsf va00 va01
			endi
			addv ov97 1
		else
			setv ov99 0
		endi
	endi
endm

****************************
* Removers

* ball remover
rscr
enum 2 21 32314
	kill targ
next
scrx 2 21 32314 1
scrx 2 21 32314 4
scrx 2 21 32314 6

* cannon remover
enum 2 21 32300
	kill targ
next
scrx 2 21 32300 1
scrx 2 21 32300 4
scrx 2 21 32300 6
scrx 2 21 32300 9

enum 2 21 32315
	kill targ
next
scrx 2 21 32315 9

* pump remover
enum 2 21 32333
	kill targ
next
enum 1 5 32333
	kill targ
next
scrx 2 21 32333 1

* remove pianola
enum 2 21 1021
	kill targ
next
scrx 2 21 1021 1
scrx 2 21 1021 124

* top remover
enum 2 21 1160
	kill targ
next
scrx 2 21 1160 1
scrx 2 21 1160 9