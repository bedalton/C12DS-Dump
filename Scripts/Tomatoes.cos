* C1 Tomatoes

* Dependencies:
*  c1plants.c16
*  chwp.wav

inst
lock

new: vhcl 2 4 32302 "c1plants" 1 23 100
attr 735
cabn -5 7 25 30
accg 5
fric 100
aero 10
elas 0
bhvr 0
perm 60
tick rand 90 100
mesg wrt+ targ 1000 0 0 0

doif game "C1_to_DS_Loaded" = 1 and totl 2 4 32302 <= 1
* 	Tomato plant
	mvsf 2696 10542
	attr 196
elif type game "ds_game_type" ne 2
* 	it's NOT a string, this must be C3 only
* 	choose a safe C3 location
	setv va00 5440
	setv va01 3580
	velo rand 30 40 -5
	mvsf va00 va01
else
* 	it IS a string, but what is the value?
	doif game "ds_game_type" eq "undocked"
* 		this is an undocked world, location needs to be DS safe
		setv va00 6100
		setv va01 9200
		velo rand 30 40 -5
		mvsf va00 va01
	elif game "ds_game_type" eq "docked"
* 		this is a docked world, location can be anywhere in the DS/C3 realm
		setv va00 6100
		setv va01 9200
		velo rand 30 40 -5
		mvsf va00 va01
	endi
endi

***********
* Scripts

scrp 2 4 32302 1
*	Tomato plant clicked
	inst
	doif game "C1_Clickable_Food" = 1
		mesg wrt+ ownr 1000 1 0 0
	endi
endm

scrp 2 4 32302 9
*	Tomato plant timer
	inst
	mesg wrt+ ownr 1000 0 0 0
endm

scrp 2 4 32302 125
*	Tomato plant lost tomato
	inst
	mesg wrt+ ownr 1000 0 0 rand 20 110
endm


scrp 2 4 32302 1000
*	Tomato plant's timer
	inst
	doif movs = 0
		setv va20 0
		epas 2 8 32398
			addv va20 1
		next

		doif va20 < 1 or _p1_ = 1
*		no fruit exists, so better make one
			setv va00 posl
			setv va01 post
			new: simp 2 8 32398 "c1plants" 1 24 101
			mira 1
			addv va00 -4
			addv va01 15
			emit 6 .20
			mvsf va00 va01
			seta va20 targ
			accg 0
			attr 199
			fric 100
			elas 0
			aero 100
			bhvr 48
			perm 60
			targ ownr
			doif _p1_ <> 1
				setv va52 0
				loop
					spas ownr va20
					wait 1
					inst
					addv va52 1
					targ va20
				untl targ = null or carr = ownr or va52 > 10
				inst
			else
				targ va20
				velo rand -10 10 rand 1 5
			endi
			accg 5
			fric 100
			aero 10
			attr 199
		endi
	endi
endm


scrp 2 8 32398 1
*	Tomato clicked
	inst
	doif game "C1_Clickable_Food" = 1
		setv va00 posl
		setv va01 post
		new: simp 2 8 32398 "c1plants" 1 24 101
		mira 1
		emit 6 .20
		seta va20 targ
		elas 0
		bhvr 48
		perm 60
		accg 5
		fric 100
		aero 10
		attr 199
		mvsf va00 va01
		velo rand -15 15 rand -15 -5
	endi
endm

scrp 2 8 32398 4
*	Tomato - I've been picked up
	inst
	lock
	mira rand 0 1
	attr 199
	accg 3
	fric 90
	aero 10
endm

scrp 2 8 32398 12
*	eating a fruit
*	Tomato
	inst
	lock
	seta va20 norn
	targ from
	doif crea targ = 1
		chem 5 0.9
		chem 161 1.0
		chem 116 0.1
*		chem 12 1.0
*		chem 34 0.5
*		chem 39 1.0
*		chem 34 1.0
*		chem 98 0.5
*		chem 99 0.5
*		chem 128 -0.5
		snde "chwp"
		stim writ from 78 1
	endi
	targ ownr
	kill targ
endm

rscr

enum 2 4 32302
	kill targ
next
scrx 2 4 32302 1
scrx 2 4 32302 9
scrx 2 4 32302 125
scrx 2 4 32302 1000

enum 2 8 32398
	kill targ
next
*scrx 2 8 32398 1
*scrx 2 8 32398 4
*scrx 2 8 32398 12