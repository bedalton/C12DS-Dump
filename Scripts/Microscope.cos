****************************
* C1 Microscope
****************************

* Dependencies:

* Installers
inst

new: comp 2 21 32317 "c1microscope" 7 0 200
attr 199
accg 3
bhvr 33
fric 90
aero 0
elas 0
perm 70

doif game "C1_to_DS_Loaded" = 1 and totl 2 21 32317 <= 1
* 	new microscope in norn house
	mvsf 2482 11058
elif type game "ds_game_type" ne 2
* 	it's NOT a string, this must be C3 only
* 	choose a safe C3 location
	setv va00 5440
	setv va01 3580
	velo rand 30 40 -5
	mvsf va00 va01
else
* 	it IS a string, but what is the value?
	doif game "ds_game_type" eq "undocked"
* 		this is an undocked world, location needs to be DS safe
		setv va00 6100
		setv va01 9200
		velo rand 30 40 -5
		mvsf va00 va01
	elif game "ds_game_type" eq "docked"
* 		this is a docked world, location can be anywhere in the DS/C3 realm
		setv va00 6100
		setv va01 9200
		velo rand 30 40 -5
		mvsf va00 va01
	endi
endi

****************************
* Scripts

scrp 2 21 32317 1
*	microscope is pushed
	inst
	lock
	sndc "c1g2"
	stim writ from 97 1
endm

scrp 2 21 32317 6
*	microscope collides
	inst
	snde "c1d2"
endm


****************************
* Removers

* microscope remover
rscr
enum 2 21 32317
	kill targ
next
scrx 2 21 32317 1
scrx 2 21 32317 6